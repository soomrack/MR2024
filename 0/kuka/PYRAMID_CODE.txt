DEF  Andreev ( )

;FOLD DECL
DECL INT COUNTER 
DECL INT CUBE
DECL INT CUBE_DISTANCE

DECL FRAME CUBE_POS 
DECL FRAME VERT_MOVE

DECL FRAME START_PYRAMID

DECL FRAME NEW_HOME
;ENDFOLD

;FOLD INI
;FOLD BASISTECH INI
GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
INTERRUPT ON 3 
BAS (#INITMOV,0 )
;ENDFOLD (BASISTECH INI)
;FOLD USER INI
CUBE = 30
CUBE_DISTANCE = 10     

CUBE_POS = {X 0, Y 0, Z 0, A 0, B 0, C 0}

START_PYRAMID = {X 0, Y 300, Z 0, A 0, B 0, C 0}

NEW_HOME = {X 94.9, Y 862.6, Z 43.9, A 146, B 0, C 0}
;ENDFOLD (USER INI)
;ENDFOLD (INI)

;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD

;FOLD MAIN

GRAB_OPEN()
$BASE = $WORLD
$BASE = NEW_HOME

FOR COUNTER = 1 TO 6 STEP 1
   
   CUBE_POS.X = CUBE_POS.X - CUBE - CUBE_DISTANCE
   
   VERT_MOVE = CUBE_POS
   VERT_MOVE.Z = VERT_MOVE.Z + 60
   
   PTP VERT_MOVE
   PTP CUBE_POS
   GRAB_CLOSE()
   PTP VERT_MOVE
   
   VERT_MOVE = START_PYRAMID
   VERT_MOVE.Z = VERT_MOVE.Z + 60
   
   PTP VERT_MOVE
   PTP START_PYRAMID
   GRAB_OPEN()
   PTP VERT_MOVE
   
   SWITCH COUNTER
      CASE 1,2
         START_PYRAMID.X = START_PYRAMID.X + 35
      CASE 3
         START_PYRAMID.Z = START_PYRAMID.Z + 30
         START_PYRAMID.X = START_PYRAMID.X - 17.5
      CASE 4
         START_PYRAMID.X = START_PYRAMID.X - 35
      CASE 5
         START_PYRAMID.Z = START_PYRAMID.Z + 30
         START_PYRAMID.X = START_PYRAMID.X + 17.5
      DEFAULT
         GRAB_OPEN()
   ENDSWITCH
ENDFOR

FOR COUNTER = 1 TO 6 STEP 1
   
   VERT_MOVE = START_PYRAMID
   VERT_MOVE.Z = VERT_MOVE.Z + 60
   
   PTP VERT_MOVE
   PTP START_PYRAMID
   GRAB_CLOSE()
   PTP VERT_MOVE
   
   VERT_MOVE = CUBE_POS
   VERT_MOVE.Z = VERT_MOVE.Z + 60
   
   PTP VERT_MOVE
   PTP CUBE_POS
   GRAB_OPEN()
   PTP VERT_MOVE
   
   CUBE_POS.X = CUBE_POS.X + CUBE + CUBE_DISTANCE
   
   SWITCH COUNTER
      CASE 1
         START_PYRAMID.Z = START_PYRAMID.Z - 30
         START_PYRAMID.X = START_PYRAMID.X - 17.5
      CASE 2
         START_PYRAMID.X = START_PYRAMID.X + 35
      CASE 3
         START_PYRAMID.Z = START_PYRAMID.Z - 30
         START_PYRAMID.X = START_PYRAMID.X + 17.5
      CASE 4,5 
         START_PYRAMID.X = START_PYRAMID.X - 35
      DEFAULT 
         GRAB_OPEN()
         PTP NEW_HOME
   ENDSWITCH
ENDFOR

;ENDFOLD

;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD

END

DEF GRAB_CLOSE ()
	$OUT[3]=TRUE
	WAIT SEC 1
	$OUT[3]=FALSE
END

DEF GRAB_OPEN ()
	$OUT[4]=TRUE
	WAIT SEC 1
	$OUT[4]=FALSE
END