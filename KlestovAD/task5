#include <DHT11.h>

#define EXTERNAL_LIGHT_PIN A0
#define SOIL_HUMI_PIN A1
#define AIR1_PIN 12

#define WATER_PUMP_PIN 5
#define LED_LIGHT_PIN 6
#define FAN_PIN 7
#define HEAT_PIN 4

struct Climate {
  int temperature_min, temperature_max;
  int soil_humidity_min, soil_humidity_max;
  int air_humidity_min, air_humidity_max;
  int light_min, light_max;
  unsigned long pump_work_time, pump_dead_time;
  unsigned long light_duration;
  int start_ventilation, end_ventilation;
  unsigned long ventilation_duration;
  int sleep_start, sleep_end;
};

const Climate tomatoes = {
  240, // Min temperature
  260, // Max temperature
  550, // Min soil humidity
  540, // Max soil humidity
  20, // Min air humidity
  30, // Max air humidity
  600, // Min light
  700, // Max light
  20000, // Pump work time
  20000, // Pump dead tome
  1000, // Light duration
  16, // Start ventilation, at
  17, // End ventilation, at
  60000, // Duration of ventilation
  20, // Start sleeping, at
  8 // End sleeping, at
};

struct Timer {
  unsigned long cur_millis;
  unsigned int cur_hour, cur_min, cur_sec;

  void set(unsigned int h, unsigned int m, unsigned int s) {
    cur_hour = h; cur_min = m; cur_sec = s;
  }
  void update() {
    static unsigned long last_millis = 0;
    cur_millis = millis();
    if ((cur_millis - last_millis) >= 1000) {
      cur_sec += (cur_millis - last_millis) / 1000;
      last_millis = cur_millis;
    }
    if (cur_sec >= 60) { cur_min++; cur_sec -= 60; }
    if (cur_min == 60) { cur_hour++; cur_min = 0; }
    if (cur_hour == 24) cur_hour = 0;
  }
  void print() {
    Serial.print("Time: ");
    Serial.print(cur_hour); Serial.print(".");
    Serial.print(cur_min); Serial.print(".");
    Serial.println(cur_sec);
  }
};

class SensorManager {
  DHT11 dht1;
public:
  unsigned int ext_light = 0, soil_humidity = 0, air_humidity = 0;
  unsigned int air_temp1 = 0

  SensorManager() : dht1(AIR1_PIN) {}

  void readAll() {
    ext_light = analogRead(EXTERNAL_LIGHT_PIN);
    soil_humidity = analogRead(SOIL_HUMI_PIN);
    air_humidity = dht1.readHumidity();
    air_temp1 = dht1.readTemperature();
  }
  void print() {
    Serial.println("------------------------------");
    Serial.print("Light: "); Serial.println(ext_light);
    Serial.print("Soil humidity: "); Serial.println(soil_humidity);
    Serial.print("Air humidity: "); Serial.println(air_humidity);
    Serial.print("Air temperature: "); Serial.println(air_temp1);
  }
};

class ActuatorManager {
public:
  bool is_light = false, is_watering = false, is_airing = false, is_heating = false;
  bool allow_heating = true;

  void apply() {
    digitalWrite(LED_LIGHT_PIN, is_light ? HIGH : LOW);
    digitalWrite(WATER_PUMP_PIN, is_watering ? HIGH : LOW);
    digitalWrite(FAN_PIN, is_airing ? HIGH : LOW);
    digitalWrite(HEAT_PIN, is_heating ? HIGH : LOW);
  }
  void printStatus() {
    Serial.println("------------------------------");
    Serial.print("Status temp: "); Serial.println(digitalRead(HEAT_PIN));
    Serial.print("Status pump: "); Serial.println(digitalRead(WATER_PUMP_PIN));
    Serial.print("Allow heating: "); Serial.println(allow_heating);
  }
};

class ClimateController {
  const Climate& climate;
  SensorManager& sensors;
  ActuatorManager& actuators;
  Timer& timer;

  unsigned long t_light = 0, p_time = 0, dead_time_start = 0, t_air = 0, t_heat = 0;
public:
  ClimateController(const Climate& c, SensorManager& s, ActuatorManager& a, Timer& r)
    : climate(c), sensors(s), actuators(a), timer(r) {}

  void control() {
    controlLight();
    controlWater();
    controlAir();
    controlTemp();
  }

  void controlLight() {
    if ((timer.cur_hour < climate.sleep_end) || (timer.cur_hour >= climate.sleep_start)) {
      actuators.is_light = false;
      return;
    }
    if ((sensors.ext_light >= climate.light_min) && !digitalRead(LED_LIGHT_PIN)) {
      actuators.is_light = true;
      t_light = timer.cur_millis;
    }
    if ((timer.cur_millis - t_light >= climate.light_duration) && (sensors.ext_light <= climate.light_max)) {
      actuators.is_light = false;
    }
  }

  void controlWater() {
    if ((sensors.soil_humidity >= climate.soil_humidity_min) && !digitalRead(WATER_PUMP_PIN)
        && (timer.cur_millis - dead_time_start > climate.pump_dead_time)) {
      actuators.allow_heating = false;
      actuators.is_watering = true;
      p_time = timer.cur_millis;
    }
    if (digitalRead(WATER_PUMP_PIN) && (timer.cur_millis - p_time >= climate.pump_work_time)
        && (sensors.soil_humidity <= climate.soil_humidity_max)) {
      actuators.allow_heating = true;
      actuators.is_watering = false;
      dead_time_start = timer.cur_millis;
    }
  }

  void controlAir() {
    if ((timer.cur_hour >= climate.start_ventilation) && (timer.cur_hour < climate.end_ventilation)) {
      actuators.is_airing = true;
      return;
    }
    if ((sensors.air_humidity >= climate.air_humidity_max) && !digitalRead(FAN_PIN)) {
      actuators.is_airing = true;
      t_air = timer.cur_millis;
      return;
    }
    if ((timer.cur_millis - t_air >= climate.ventilation_duration) && (sensors.air_humidity <= climate.air_humidity_min)) {
      actuators.is_airing = false;
      return;
    }
  }

  void controlTemp() {
    if (!actuators.allow_heating) {
      actuators.is_airing = false;
      actuators.is_heating = false;
      return;
    }
    if ((sensors.air_temp1 <= climate.temperature_min) && !digitalRead(FAN_PIN)) {
      actuators.is_airing = true;
      actuators.is_heating = true;
      t_heat = timer.cur_millis;
    }
    if ((timer.cur_millis - t_heat >= climate.ventilation_duration) && (sensors.air_temp1 >= climate.temperature_max)) {
      actuators.is_airing = false;
      actuators.is_heating = false;
    }
  }
};

void pin_init() {
  pinMode(EXTERNAL_LIGHT_PIN, INPUT);
  pinMode(SOIL_HUMI_PIN, INPUT);
  pinMode(WATER_PUMP_PIN, OUTPUT);
  pinMode(LED_LIGHT_PIN, OUTPUT);
  pinMode(FAN_PIN, OUTPUT);
  pinMode(HEAT_PIN, OUTPUT);
}

Timer timer;
SensorManager sensors;
ActuatorManager actuators;
ClimateController controller(tomatoes, sensors, actuators, timer);

void setup() {
  pin_init();
  timer.set(16, 59, 46);
  Serial.begin(9600);
}

void loop() {
  sensors.readAll();
  timer.update();
  controller.control();
  actuators.apply();

  timer.print();
  actuators.printStatus();
  sensors.print();

  delay(1000);
}
